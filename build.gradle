
plugins {
    id 'fabric-loom' version '1.4-SNAPSHOT'
    id 'maven-publish'
    id "com.modrinth.minotaur" version "2.+"
}

version = project.mod_version
group = project.maven_group

base {
    archivesName = project.archives_base_name
}

repositories {
    maven { url "https://s01.oss.sonatype.org/content/repositories/snapshots/" }
    maven { url "https://oss.sonatype.org/content/repositories/snapshots" }
    maven { url "https://jitpack.io" }
    maven { url 'https://masa.dy.fi/maven' }
    maven { url 'https://maven.nucleoid.xyz' }
    maven { url 'https://projectlombok.org/edge-releases' }
}

loom {
// fix: adventure mod error in runServer()
//    splitEnvironmentSourceSets()

    accessWidenerPath = file("src/main/resources/fuji.accesswidener")
    mods {
        "fuji" {
            sourceSet sourceSets.main
        }
    }

    runs {
        configureEach {
            vmArgs("-Xms16G -Xmx16G -XX:+UseG1GC -XX:+ParallelRefProcEnabled -XX:MaxGCPauseMillis=200 -XX:+UnlockExperimentalVMOptions -XX:+DisableExplicitGC -XX:+AlwaysPreTouch -XX:G1NewSizePercent=40 -XX:G1MaxNewSizePercent=50 -XX:G1HeapRegionSize=16M -XX:G1ReservePercent=15 -XX:G1HeapWastePercent=5 -XX:G1MixedGCCountTarget=4 -XX:InitiatingHeapOccupancyPercent=15 -XX:G1MixedGCLiveThresholdPercent=90 -XX:G1RSetUpdatingPauseTimePercent=5 -XX:SurvivorRatio=32 -XX:+PerfDisableSharedMem -XX:MaxTenuringThreshold=1 -XX:+UseLargePages -XX:+UseTransparentHugePages -Duser.timezone=GMT+08".split(" "))
        }
    }
}

dependencies {
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings loom.officialMojangMappings()
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

    compileOnly "org.projectlombok:lombok:${project.lombok_version}"
    annotationProcessor "org.projectlombok:lombok:${project.lombok_version}"
    testCompileOnly "org.projectlombok:lombok:${project.lombok_version}"
    testAnnotationProcessor "org.projectlombok:lombok:${project.lombok_version}"

    implementation group: 'com.google.code.gson', name: 'gson', version: "${project.gson_version}"

    include(implementation(annotationProcessor("com.github.llamalad7.mixinextras:mixinextras-fabric:${project.mixin_extras_version}")))

    modImplementation("carpet:fabric-carpet:${project.carpet_core_version}")

    modImplementation include("me.lucko:fabric-permissions-api:${project.fabric_permissions_version}")
    modImplementation include("net.kyori:adventure-platform-fabric:${project.adventure_platform_fabric_version}")
    include(implementation "net.kyori:adventure-text-minimessage:${project.adventure_api_version}")
    include(implementation "net.kyori:adventure-text-serializer-plain:${project.adventure_api_version}")

    include(implementation group: 'org.apache.commons', name: 'commons-compress', version: "${project.apache_commons_compression_version}")
    include(implementation group: 'commons-io', name: 'commons-io', version: "${project.apache_commons_io_version}")

    include(implementation group: 'org.quartz-scheduler', name: 'quartz', version: "${project.quartz_version}")

    modImplementation include("eu.pb4:sgui:${project.sgui_version}")

    implementation "me.lucko:spark-api:${project.spark_version}"
    
    include(implementation "org.reflections:reflections:${project.reflections_version}")
    include(implementation "org.javassist:javassist:${project.javaassist_version}")
}

processResources {
    inputs.property "version", project.version
    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

tasks.withType(JavaCompile).configureEach {
    it.options.release.set(17)
}

java {
    // Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
    // if it is present.
    // If you remove this line, sources will not be generated.
    withSourcesJar()
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

jar {
    from("LICENSE") {
        rename { "${it}-mc${minecraft_version}-${project.base.archivesName.get()}" }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }

    // See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
    repositories {
        // Add repositories to publish to here.
        // Notice: This block does NOT have the same function as the block in the top level.
        // The repositories here will be used for publishing your artifact, not for
        // retrieving dependencies.
    }
}

// build.gradle
modrinth {
    token = "$System.env.modrinth_token"
    projectId = "1TowMm2v"
    versionNumber = "$mod_version"
    versionType = "release"
    uploadFile = remapJar
    gameVersions = ["1.20.1"]
    loaders = ["fabric"]
    dependencies {
        required.project "fabric-api"
        optional.project "carpet"
        optional.project "spark"
    }
 syncBodyFrom = rootProject.file("README.md").text
}
tasks.modrinth.dependsOn(tasks.modrinthSyncBody)
tasks.register('replaceReadme') {
    // todo
}
tasks.modrinthSyncBody.dependsOn(tasks.replaceReadme)
